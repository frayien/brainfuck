
This code replace all vowels (aeiouy) by themselves twice with 'p' in the middle (*p* where * is the vowel)

exemple :
"abcdefghijklmnopqrstuvwxyz"
become
"apabcdepefghipijklmnopopqrstupuvwxypyz"

by frayien

>> goto 2
----------[++++++++++>,----------] read input
<[<]> goto 2 (first char of input)
[
    check 'a' (97)
    <[-]++++++++++[>----------<-]>+++ moins 97

    // check if 0 (if 0 then char was 'a')
    <[-]+ set tmp1 to 1
    <[-]  set tmp0 to 0
    >> goto char
    [ while char not 0
        <[-] set tmp1 to 0
        <+ inc tmp0
        >>- dec char
    ]
    << goto tmp0
    [>>+<<-] move tmp0 to char
    > goto tmp1
    [ if tmp1 not 0 then char was 0
        > goto char
        <[-]++++++++++[>++++++++++<-]>---. set 'a' (97) and print
        ++++++++++ +++++. set 'p' (112) and print
        [-] set 0
        <[-] goto tmp1 and set 0
    ]
    > goto char
    <[-]++++++++++[>++++++++++<-]>--- add 97 (back to initial state)

    check 'e' (101)
    <[-]++++++++++[>----------<-]>-

    <[-]+
    <[-]>
    >[<[-]<+>>-]<<[>>+<<-]>
    [  >
        <[-]++++++++++[>++++++++++<-]>+.
        ++++++++++ +.
        [-]
        <[-]
    ]>

    <[-]++++++++++[>++++++++++<-]>+

    check 'i' (105)
    <[-]++++++++++[>----------<-]>-----

    <[-]+
    <[-]>
    >[<[-]<+>>-]<<[>>+<<-]>>
    <[  >
        <[-]++++++++++[>++++++++++<-]>+++++.
        +++++++.
        [-]
        <[-]
    ]>

    <[-]++++++++++[>++++++++++<-]>+++++

    check 'o' (111)
    <[-]+++++++++++[>----------<-]>-

    <[-]+
    <[-]>
    >[<[-]<+>>-]<<[>>+<<-]>>
    <[  >
        <[-]+++++++++++[>++++++++++<-]>+.
        +.
        [-]
        <[-]
    ]>

    <[-]+++++++++++[>++++++++++<-]>+

    check 'u' (117)
    <[-]++++++++++++[>----------<-]>+++

    <[-]+
    <[-]>
    >[<[-]<+>>-]<<[>>+<<-]>>
    <[  >
        <[-]++++++++++++[>++++++++++<-]>---.
        -----.
        [-]
        <[-]
    ]>

    <[-]++++++++++++[>++++++++++<-]>---

    check 'y' (121)
    <[-]++++++++++++[>----------<-]>-

    <[-]+
    <[-]>
    >[<[-]<+>>-]<<[>>+<<-]>>
    <[  >
        <[-]++++++++++++[>++++++++++<-]>+.
        ---------.
        [-]
        <[-]
    ]>

    <[-]++++++++++++[>++++++++++<-]>+
    .
>]